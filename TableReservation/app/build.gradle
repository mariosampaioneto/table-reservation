buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath 'me.tatarka:gradle-retrolambda:3.2.5'
    }
}

apply plugin: 'com.android.application'
apply plugin: 'me.tatarka.retrolambda'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.3"

    defaultConfig {
        applicationId "com.mfactory.tablereservation"
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 16100
        // 16 -> minimum sdk, 100 -> current version. ABI version added to start of versionCode, ex: x86_64 -> 916100
        versionName "1.0.0"
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        vectorDrawables.useSupportLibrary = true

        setProperty("archivesBaseName", "table-reservations-$versionName")

        buildConfigField "String", "SERVER_URL", '"https://s3-eu-west-1.amazonaws.com/quandoo-assessment/"'
    }

    splits {
        abi {
            enable gradle.startParameter.taskNames.contains("assembleRelease")
            reset()
            include 'armeabi', 'armeabi-v7a', 'arm64-v8a', 'mips', 'x86', 'x86_64'
            //select ABIs to build APKs for, small apk size for each ABI
            universalApk true //generate an additional APK that contains all the ABIs
        }
    }

    project.ext.versionCodes = ['armeabi': 1, 'armeabi-v7a': 2, 'arm64-v8a': 3, 'mips': 5, 'mips64': 6, 'x86': 8, 'x86_64': 9]

    android.applicationVariants.all { variant ->
        variant.outputs.each { output ->
            output.versionCodeOverride =
                    project.ext.versionCodes.get(output.getFilter(
                            com.android.build.OutputFile.ABI), 0) * 100000 + android.defaultConfig.versionCode
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            useProguard false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            useProguard true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    testOptions.unitTests.all {
        testLogging {
            events 'passed', 'skipped', 'failed', 'standardOut', 'standardError'
        }
    }
}

ext {
    supportVersion = "25.3.1"
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])

    //Google
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile "com.android.support:support-v4:${supportVersion}"
    compile "com.android.support:design:${supportVersion}"
    compile "com.android.support:recyclerview-v7:${supportVersion}"
    compile "com.android.support:cardview-v7:${supportVersion}"
    compile "com.android.support.constraint:constraint-layout:1.0.2"

    //View bind
    compile 'com.jakewharton:butterknife:8.4.0'
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.4.0'

    //Dialogs
    compile 'com.afollestad.material-dialogs:core:0.9.4.3'

    //Fonts
    compile 'uk.co.chrisjenx:calligraphy:2.3.0'

    //Dagger
    compile 'com.google.dagger:dagger:2.10'
    compile 'com.google.dagger:dagger-android:2.10'
    compile 'com.google.dagger:dagger-android-support:2.10'
    annotationProcessor 'com.google.dagger:dagger-compiler:2.10'
    annotationProcessor 'com.google.dagger:dagger-android-processor:2.10'
    provided 'org.glassfish:javax.annotation:10.0-b28'

    //RxJava
    compile 'io.reactivex.rxjava2:rxjava:2.0.7'
    compile 'io.reactivex.rxjava2:rxandroid:2.0.1'

    //Retrofit + OKHttp + GSON
    compile 'com.squareup.retrofit2:retrofit:2.2.0'
    compile 'com.squareup.retrofit2:converter-gson:2.2.0'
    compile 'com.squareup.retrofit2:adapter-rxjava2:2.2.0'
    compile 'com.squareup.okhttp3:okhttp:3.6.0'
    compile 'com.squareup.okhttp3:logging-interceptor:3.4.1'
    compile 'com.google.code.gson:gson:2.8.0'

    //Cache
    compile 'com.orhanobut:hawk:2.0.1'

    //Logs
    compile 'com.jakewharton.timber:timber:4.5.1'

    //Job scheduler
    compile 'com.firebase:firebase-jobdispatcher:0.8.2'


    testCompile 'junit:junit:4.12'
}
